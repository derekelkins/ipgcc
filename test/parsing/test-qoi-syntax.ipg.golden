QOI
  -> QOIHeader@0[0, EOI]
     { width = QOIHeader@0.width }
     { height = QOIHeader@0.height }
     { channels = QOIHeader@0.channels }
     { colorspace = QOIHeader@0.colorspace }
     QOIChunks@0[QOIHeader@0.END, EOI]
     { chunks = QOIChunks@0.values };

QOIHeader
  -> "qoif"[0, 4]
     BE_U32@0[4, EOI]
     { width = BE_U32@0.value }
     BE_U32@1[BE_U32@0.END, EOI]
     { height = BE_U32@1.value }
     { channels = .[BE_U32@1.END] }
     { colorspace = .[BE_U32@1.END + 1] };

QOIChunks
  -> repeat QOIChunk@0[QOIChunk@0.END, EOI].chunk starting on [0, EOI] until EndMarker@0;

QOIChunk
  -> { tagByte = .[0] }
     Chunk@0(tagByte)[1, EOI]
     { chunk = Chunk@0.this };

Chunk(tagByte)
  -> ?[ tagByte == 254 ]
     { tag = "rgb" }
     { r = .[0] }
     { g = .[1] }
     { b = .[2] }
   / ?[ tagByte == 255 ]
     { tag = "rgba" }
     { r = .[0] }
     { g = .[1] }
     { b = .[2] }
     { a = .[3] }
   / ?[ tagByte >> 6 == 0 ]
     { tag = "index" }
     { index = tagByte & 63 }
   / ?[ tagByte >> 6 == 1 ]
     { tag = "diff" }
     { dr = (tagByte >> 4 & 3) - 2 }
     { dg = (tagByte >> 2 & 3) - 2 }
     { db = (tagByte & 3) - 2 }
   / ?[ tagByte >> 6 == 2 ]
     { tag = "luma" }
     { diffGreen = (tagByte & 63) - 32 }
     U8@0[0, EOI]
     { drdg = (U8@0.value >> 4) - 8 }
     { dbdg = (U8@0.value & 15) - 8 }
   / ?[ tagByte >> 6 == 3 ]
     { tag = "run" }
     { run = (tagByte & 63) + 1 };

EndMarker
  -> "\x00\x00\x00\x00\x00\x00\x00\x01"[0, 8];

U8
  -> { value = .[0] };

BE_U32
  -> { bs = *[0, 4] }
     { value = bs[3] | bs[2] << 8 | bs[1] << 16 | bs[0] << 24 };